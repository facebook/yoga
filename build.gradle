/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

plugins {
    id("com.android.library") version "8.7.1" apply false
    id("com.android.application") version "8.7.1" apply false
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
    id 'org.jetbrains.kotlin.android' version '2.1.20' apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

group = "com.facebook.yoga"

if (project.hasProperty("USE_SNAPSHOT") && project.property("USE_SNAPSHOT").toBoolean()) {
    version = getProperty("VERSION_NAME") + "-SNAPSHOT"
} else {
    version = getProperty("VERSION_NAME")
}

def sonatypeUsername = findProperty("SONATYPE_USERNAME")?.toString()
def sonatypePassword = findProperty("SONATYPE_PASSWORD")?.toString()

nexusPublishing {
    repositories {
        sonatype {
            username.set(sonatypeUsername)
            password.set(sonatypePassword)
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
